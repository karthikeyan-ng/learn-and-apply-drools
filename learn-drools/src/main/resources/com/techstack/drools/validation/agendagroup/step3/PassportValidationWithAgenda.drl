package com.techstack.drools.validation.agendagroup.step3

import com.techstack.drools.domain.Passport;
import com.techstack.drools.domain.Validation;
import java.time.LocalDate;

rule "Valid passport"
	dialect "mvel"
	agenda-group "valid-passport"
	when
		$passport: Passport( validation == Validation.UNKNOWN )
	then
		System.out.println( $passport + " is without validation info, consider OK for now" );
		modify($passport) {
		  setValidation( Validation.PASSED )
		}
end

rule "Expired passport"
	dialect "mvel"
	agenda-group "invalid-passport"
	when
		$passport: Passport( isExpired()  )
	then
		System.out.println( $passport + " is INVALID because it is expired" );
		modify($passport) {
		  setValidation( Validation.FAILED ),
		  setCause( "passport is expired" )
		}
end

rule "Full passport"
	dialect "mvel"
	agenda-group "invalid-passport"
	when
		$passport: Passport( unusedVisaPages == 0   )
	then
		System.out.println( $passport + " is INVALID because it has no space for visa" );
		modify($passport) {
		  setValidation( Validation.FAILED ),
		  setCause( "passport has no space for visa" )
		}
end
