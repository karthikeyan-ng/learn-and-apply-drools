package com.techstack.drools.validation.crossproducts.step3

import com.techstack.drools.domain.Passport;
import com.techstack.drools.domain.Validation;
import com.techstack.drools.domain.VisaApplication;

import java.time.LocalDate;

/*
In the prvious Step2 the same rule was executed over and over.
To avoid such a Rule execution, you can nest strict conditions
as shown this business rule.

passportNumber == $passport.passportNumber && validation != Validation.FAILED
*/
rule "Invalidate visa application with invalid passport"
  dialect "mvel"
  when
    $passport: Passport( validation == Validation.FAILED )
    $visaApplication: VisaApplication( passportNumber == $passport.passportNumber,
                                       validation != Validation.FAILED )
  then
    System.out.println( "Set " + $visaApplication + " invalid as " + $passport + " hasn't passed validation");
    modify($visaApplication) {
      setValidation( Validation.FAILED )
    }
end

rule "Set application without validation info as passed"
  dialect "mvel"
  when
    $visaApplication: VisaApplication( validation == Validation.UNKNOWN )
  then
    System.out.println( $visaApplication + " is without validation info, consider OK for now" );
    modify($visaApplication) {
      setValidation( Validation.PASSED )
    }
end

rule "Invalidate visa application where passport expires earlier than 6 months after visit end date"
  dialect "mvel"
  when
    $passport: Passport( validation != Validation.FAILED )
    $visaApplication: VisaApplication( passportNumber == $passport.passportNumber,
                                       $passport.expiresOn.isBefore(visitEndDate.plusMonths(6)),
                                       validation != Validation.FAILED )
  then
    System.out.println( "Set " + $visaApplication + " invalid as " + $passport + " not valid 6 months after visit");
    modify($visaApplication) {
     setValidation( Validation.FAILED )
    }
end
