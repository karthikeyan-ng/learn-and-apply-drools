package com.techstack.drools.validation.group.step4

import com.techstack.drools.domain.GroupLeader;
import com.techstack.drools.domain.InvalidPassport;
import com.techstack.drools.domain.Passport;
import com.techstack.drools.domain.InvalidFamilyVisaApplication;
import com.techstack.drools.domain.FamilyVisaApplication;

import java.time.LocalDate;

rule "Invalidate visa applications with invalid passport"
	dialect "mvel"
	when
		$familyVisaApplication: FamilyVisaApplication( )
		exists InvalidPassport( $familyVisaApplication.passportNumbers.contains(this.passport.passportNumber) )
	then
		System.out.println( "Set " + $familyVisaApplication + " invalid as it contains invalid passport");
		insert( new InvalidFamilyVisaApplication($familyVisaApplication) )
end

rule "Invalidate visa application where everyone is under 16"
	dialect "mvel"
	when
		$familyVisaApplication: FamilyVisaApplication( )
		forall (
		  $passport : Passport( $familyVisaApplication.passportNumbers.contains(this.passportNumber) ),
					  Passport( this == $passport, this.age < 16)
		)
	then
		System.out.println( "Set " + $familyVisaApplication + " invalid as everyone is younger than 16");
		insert( new InvalidFamilyVisaApplication($familyVisaApplication) )
end

rule "Choose the leader for the group of people under 18"
	dialect "mvel"
	when
		$familyVisaApplication: FamilyVisaApplication( )
		forall (
		  $passport : Passport( $familyVisaApplication.passportNumbers.contains(this.passportNumber) ),
					  Passport( this == $passport, age < 18)
		)
		$groupLeaderPassport: Passport( $familyVisaApplication.passportNumbers.contains(this.passportNumber) )
		forall (
		  $passport : Passport( $familyVisaApplication.passportNumbers.contains(this.passportNumber) ),
					  Passport( this == $passport, this.age <= $groupLeaderPassport.age)
		)
	then
		System.out.println( "Set " + $groupLeaderPassport + " as group leader as the oldest person in group");
		insert( new GroupLeader( $groupLeaderPassport, $familyVisaApplication) )
end
