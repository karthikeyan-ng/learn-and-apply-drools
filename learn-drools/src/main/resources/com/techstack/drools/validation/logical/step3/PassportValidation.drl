package com.techstack.drools.validation.logical.step3
 
import com.techstack.drools.domain.InvalidPassport;
import com.techstack.drools.domain.ValidPassport;
import com.techstack.drools.domain.Passport;
import com.techstack.drools.domain.Validation;
import java.time.LocalDate;

rule "Valid passport"
	dialect "mvel"
	when
		$passport: Passport( )
		not (InvalidPassport( this.passport == $passport) )
	then
		System.out.println( $passport + " is not invalid, consider OK for now" );
		insertLogical( new ValidPassport($passport) )
end

rule "Expired passport"
	dialect "mvel"
	when
		$passport: Passport( isExpired()  )
	then
		System.out.println( $passport + " is INVALID because it is expired" );
		insert( new InvalidPassport($passport) )
end

rule "Full passport"
	dialect "mvel"
	when
		$passport: Passport( unusedVisaPages == 0   )
	then
		System.out.println( $passport + " is INVALID because it has no space for visa" );
		insert( new InvalidPassport($passport) )
end
